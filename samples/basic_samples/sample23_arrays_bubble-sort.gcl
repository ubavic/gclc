dim 130 50
number NumberOfElements 12
array A { NumberOfElements }

% Initializing and printing the sequence
number i 1
while { i<=NumberOfElements }
{
	random r
	expression A[i] { r*20 }
	expression x { i*10 }
	point X x 35
	cmark X
	printvalueat_t X A[i]
	expression i { i+1 }
}

% Bubble sort
number bChanged 1
while { bChanged }
{
	expression bChanged { 0 }
	expression i { 1 }
	while { i <= NumberOfElements-1 }
	{
		if_then_else { A[i]>A[i+1] }
		{
			set_equal tmp A[i]
			set_equal A[i] A[i+1]
			set_equal A[i+1] tmp
			expression bChanged { 1 }
		}
		{
		}
		expression i { i+1 }
	}
}

% Printing the sorted sequence
number i 1
while { i<=NumberOfElements }
{
	expression x { i*10 }
	point X x 20
	cmark X
	printvalueat_b X A[i]
	expression i { i+1 }
}
