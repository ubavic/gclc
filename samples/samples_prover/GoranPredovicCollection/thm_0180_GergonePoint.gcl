% thmname Gergonova ta\ch ka
% thmnamee Gergonne point

% thmtext Upisani krug trougla $ABC$ dodiruje stranice
% thmtext trougla $BC$, $CA$ i $AB$ redom u
% thmtext ta\ch kama $D$, $E$ i $F$. Dokazati da su
% thmtext prave $AD$, $BE$ i $CF$ konkuretne.

% thmtexte Assume the incircle of $\triangle ABC$ touches the
% thmtexte sides $BC$, $AC$ and $AB$ in points
% thmtexte $D$, $E$ and $F$ respectively.
% thmtexte Show that lines $AD$, $BE$ and $CF$ meet at the Gergonne
% thmtexte point $G$ of the triangle.

dim 100 100

point A 20 20
point B 80 20
point C 70 70

bis ba B A C
bis bb C B A
bis bc A C B

line a B C
line b A C
line c A B

intersec I ba bb
foot D I a
foot E I b
foot F I c

% skip
circle k I F

line ad A D
line be B E
line cf C F

intersec G ad be

%% cmark_b I
cmark_b A
cmark_b B
cmark_t C
cmark_b F
cmark_lt E
cmark_rt D
cmark_lt G

drawcircle k
drawsegment A B
drawsegment C B
drawsegment C A
drawdashsegment A D
drawdashsegment B E
drawdashsegment C F

% prove { collinear C F G }