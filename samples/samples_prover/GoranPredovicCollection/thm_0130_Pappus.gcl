% thmname Paposova teorema
% thmnamee Pappus' Theorem

% thmtext Data je prava $p$ sa ta\ch kama
% thmtext $A$, $B$ i $C$, i ta\ch ke $A_1$ i $A_2$ van
% thmtext prave $p$. Neka je $B_1$ presek prave
% thmtext $A_1A_2$ sa pravom koja sadr\zh i $A$
% thmtext i paralelna je sa $A_1B$. Neka je $C_1$
% thmtext presek prave $A_1A_2$ sa pravom koja
% thmtext sard\zh i $B$ i paralelna je sa $CB_1$.
% thmtext Dokazati da je $AC_1$ paralelna sa $CA_1$.

% thmtexte Let $ABC$ and $A_1B_1C_1$ be two lines such that
% thmtexte $AB_1 \parallel BA_1$ and $BC_1 \parallel CB_1$.
% thmtexte Show that $AC_1 \parallel CA_1$.

dim 100 80

point A 20 10
point B 90 10
online C' A B
% skip
point C 50 10

cmark_b A
cmark_b B
cmark_b C

point A_1 55 50
point A_2 45 43
line A_1A_2 A_1 A_2

line A_1B A_1 B
parallel AB_1 A A_1B
intersec B_1 A_1A_2 AB_1

line CB_1' C' B_1
% skip
line CB_1 C B_1
parallel BC_1' B CB_1'
% skip
parallel BC_1 B CB_1
intersec C_1' A_1A_2 BC_1'
% skip
intersec C_1 A_1A_2 BC_1

cmark_lt A_1
cmark_lt B_1
cmark_lt C_1

drawline A C
drawline A_1 C_1

drawsegment A B_1
drawsegment B A_1
drawsegment B C_1
drawsegment C B_1

drawsegment A C_1
drawsegment C A_1

% prove { parallel A_1 C A C_1 }
